plugins {
    id 'com.android.library'
    id 'org.jetbrains.kotlin.android'
}


tasks.register('genUUID') {
    def uuid = UUID.randomUUID().toString()
    def odir = file("$buildDir/generated/assets/model-small-fa-0.42")
    def ofile = file("$odir/uuid")
    doLast {
        mkdir odir
        ofile.text = uuid
    }
}

preBuild.dependsOn(genUUID)

android {

    namespace 'ir.am3n.persian.asr'
    compileSdk 35

    defaultConfig {
        minSdk 21
        multiDexEnabled true
        ndk { abiFilters 'armeabi-v7a', 'arm64-v8a', 'x86_64', 'x86' }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    sourceSets {
        main {
            assets.srcDirs += "$buildDir/generated/assets"
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }
    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_17.toString()
    }

}

dependencies {

    implementation 'androidx.core:core-ktx:1.15.0'
    implementation 'androidx.appcompat:appcompat:1.7.0'
    implementation 'androidx.multidex:multidex:2.0.1'

    implementation 'net.java.dev.jna:jna:5.15.0@aar'
    implementation 'com.alphacephei:vosk-android:0.3.47@aar'

}